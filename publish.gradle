/*
 * Copyright (c) 2021. Sungbin Ji. All rights reserved.
 *
 * KakaoTalkBotBaseModule license is under the MIT license.
 * SEE LICENSE: https://github.com/sungbin5304/KakaoTalkBotBaseModule/blob/master/LICENSE
 */

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']

    pkg {
        repo = LIBRARY_REPO
        desc = LIBRARY_DESCRIPTION
        name = MODULE_NAME
        websiteUrl = SITE_URL
        vcsUrl = GIT_URL
        issueTrackerUrl = ISSUE_URL

        version {
            name = VERSION_NAME
            vcsTag = VERSION_NAME
            released = new Date()
        }

        licenses = ['MIT']
        labels = ['android']

        publish = true
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'

            groupId = GROUP
            artifactId = MODULE_NAME
            version = VERSION_NAME

            name MODULE_NAME
            description LIBRARY_DESCRIPTION
            url SITE_URL

            licenses {
                license {
                    name LICENSE_NAME
                    url LICENSE_LINK
                }
            }

            developers {
                developer {
                    id DEVELOPER_ID
                    name DEVELOPER_NAME
                    email DEVELOPER_EMAIL
                }
            }

            scm {
                url SITE_URL
                connection GIT_URL
                developerConnection GIT_URL
            }
        }
    }
}

task javadoc(type: Javadoc) {
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}